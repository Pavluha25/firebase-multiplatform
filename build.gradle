group 'io.multiplatform'
version '17.1.0-rev1'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'

repositories {
    mavenLocal()
    google()
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')

        fromPreset(presets.jvm, 'jvm') {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                jvmTarget = "1.8"
            }
        }

        fromPreset(presets.js, 'js') {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                sourceMap = true
                sourceMapEmbedSources = "always"
                moduleKind = "commonjs"
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib"
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.1.0'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-js:1.0.0'
            }
        }
        androidMain {
            dependencies {
                api 'com.google.firebase:firebase-firestore:17.1.5'
                api 'com.google.firebase:firebase-auth:16.1.0'
                api 'com.google.firebase:firebase-functions:16.1.3'
                api 'com.google.firebase:firebase-database:16.0.5'
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0'
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.1.0'
            }
        }
        jvmMain {
            kotlin.srcDir('src/androidMain/kotlin')
            dependencies {
                api "app.teamhub:firebase-java:0.1.0"
                api 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.1.0'
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0'
            }
        }
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "17.1.0-rev1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
        }
    }
}

publishing {
    repositories {
        maven {
            url myMavenRepoWriteUrl
            credentials {
                username myMavenRepoWriteUsername
                password myMavenRepoWritePassword
            }
        }
    }
}

task copyPackageJson(type: Copy) {
    from file("package.json")
    into file("${buildDir}/node_module")
}

task copyJS(type: Copy) {
    from file("${buildDir}/classes/kotlin/js/main/${project.name}.js")
    into file("${buildDir}/node_module")
    rename "${project.name}\\.js", 'index.js'
}

task publishToNpm(type:Exec, dependsOn: [copyPackageJson, copyJS]) {
    workingDir "${buildDir}/node_module"
    executable 'npm'
    args ['publish']
}