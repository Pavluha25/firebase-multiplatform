group 'io.multiplatform'
version '17.1.0-rev1'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.0-rc-116'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    google()
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')
        
        fromPreset(presets.jvm, 'jvm') {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                jvmTarget = "1.8"
            }
        }
        
        fromPreset(presets.js, 'js') {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                sourceMap = true
                sourceMapEmbedSources = "always"
                moduleKind = "commonjs"
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib"
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common:0.26.0-rc13'
            }
        }
//        jvmMain {
//            dependencies {
//                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
//        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
//            }
//        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-js:0.26.0-rc13'
            }
        }
        androidMain {
            dependencies {
                implementation 'com.google.firebase:firebase-firestore:17.1.0'
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.26.0-rc13'
            }
        }
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 28
        versionCode 1
        versionName "17.1.0-rev1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


task copyPackageJson(type: Copy) {
    from file("package.json")
    into file("${buildDir}/node_module")
}

task copyJS(type: Copy) {
    from file("${buildDir}/classes/kotlin/js/main/${project.name}.js")
    into file("${buildDir}/node_module")
    rename "${project.name}\\.js", 'index.js'
}

task publishToNpm(type:Exec, dependsOn: [copyPackageJson, copyJS]) {
    workingDir "${buildDir}/node_module"
    executable 'npm'
    args ['publish']
}